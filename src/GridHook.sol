// SPDX-License-Identifier: MIT
pragma solidity 0.8.26;

// interfaces
import {IPoolManager} from "v4-core/src/interfaces/IPoolManager.sol";
import {BaseHook} from "v4-periphery/src/base/hooks/BaseHook.sol";
// libraries
import {StateLibrary} from "v4-core/src/libraries/StateLibrary.sol";
import {Hooks} from "v4-core/src/libraries/Hooks.sol";
import {TickMath} from "v4-core/src/libraries/TickMath.sol";
// types
import {PoolId, PoolIdLibrary} from "v4-core/src/types/PoolId.sol";
import {PoolKey} from "v4-core/src/types/PoolKey.sol";
import {Currency, CurrencyLibrary} from "v4-core/src/types/Currency.sol";
import {BalanceDelta} from "v4-core/src/types/BalanceDelta.sol";
import {FixedPointMathLib} from "solmate/src/utils/FixedPointMathLib.sol";
import {ERC1155} from "solady/tokens/ERC1155.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
// Import the SafeERC20 library
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import {Ownable} from "@openzeppelin/contracts/access/Ownable.sol";


/// @title GridHook
/// @notice This contract implements a hook for managing profit-taking orders in a liquidity pool.
/// @dev Inherits from BaseHook and ERC1155 to manage token claims and orders.
contract GridHook is BaseHook, ERC1155, Ownable(msg.sender) {
    using StateLibrary for IPoolManager;

    // PoolIdLibrary used to convert PoolKeys to IDs
    using PoolIdLibrary for PoolKey;
    // Used to represent Currency types and helper functions like `.isNative()`
    using CurrencyLibrary for Currency;
    // Used for helpful math operations like `mulDiv`
    using FixedPointMathLib for uint256;

    // Errors
    error InvalidOrder();
    error InvalidRange();
    error NothingToClaim();
    error NotEnoughToClaim();
    error OnlyByPoolManager();
    error InvalidGridSpacing();


    mapping(PoolId poolId => int24 lastTick) public lastTicks;
    mapping(uint256 positionId => uint256 claimsSupply) public claimTokensSupply;
    mapping(uint256 positionId => uint256 outputClaimable) public claimableOutputTokens;
    mapping(address user => mapping(PoolId poolId => GridPosition)) public userGridPositions;
    mapping(PoolId poolId => mapping(int24 tickToSellAt => mapping(bool zeroForOne => uint256 inputAmount))) public pendingOrders;


    struct GridPosition {
        int24 lowerTick;
        int24 upperTick;
        int24 gridSpacing;
        uint256 amountPerGrid;
    }

    /// @notice Modifier to restrict access to the pool manager.
    /// @dev Reverts if the caller is not the pool manager.
    modifier onlyByPoolManager() {
        _onlyByPoolManager();
        _;
    }

    function _onlyByPoolManager() private view {
        if (msg.sender != address(poolManager)) revert OnlyByPoolManager();
    }

    /// @notice Constructor to initialize the ConditionalOrderHook contract.
    /// @param _manager The address of the pool manager.
    constructor(IPoolManager _manager) BaseHook(_manager) {}

    /// @notice Returns the permissions for the hook.
    /// @return Hooks.Permissions The permissions for the hook.
    function getHookPermissions() public pure override returns (Hooks.Permissions memory) {
        return Hooks.Permissions({
            beforeInitialize: false,
            afterInitialize: true,
            beforeAddLiquidity: false,
            afterAddLiquidity: false,
            beforeRemoveLiquidity: false,
            afterRemoveLiquidity: false,
            beforeSwap: false,
            afterSwap: true,
            beforeDonate: false,
            afterDonate: false,
            beforeSwapReturnDelta: false,
            afterSwapReturnDelta: false,
            afterAddLiquidityReturnDelta: false,
            afterRemoveLiquidityReturnDelta: false
        });
    }

    /// @notice Called after the pool is initialized.
    /// @param key The PoolKey for the pool.
    /// @param tick The current tick of the pool.
    /// @return The selector for the afterInitialize function.
    function afterInitialize(address, PoolKey calldata key, uint160, int24 tick)
        external
        override
        onlyPoolManager
        returns (bytes4)
    {
        lastTicks[key.toId()] = tick;
        return this.afterInitialize.selector;
    }

    /// @notice Called after a swap occurs in the pool.
    /// @param sender The address that initiated the swap.
    /// @param key The PoolKey for the pool.
    /// @param params The parameters for the swap.
    /// @param delta The balance delta after the swap.
    /// @param data Additional data for the swap.
    /// @return The selector for the afterSwap function and the new tick.
    function afterSwap(
        address sender,
        PoolKey calldata key,
        IPoolManager.SwapParams calldata params,
        BalanceDelta delta,
        bytes calldata data
    ) external override onlyByPoolManager returns (bytes4, int128) {
        // `sender` is the address which initiated the swap
        // if `sender` is the hook, we don't want to go down the `afterSwap`
        // rabbit hole again
        if (sender == address(this)) return (this.afterSwap.selector, 0);

        // Should we try to find and execute orders? True initially
        bool tryMore = true;
        int24 currentTick;

        while (tryMore) {
            // Try executing pending orders for this pool

            // `tryMore` is true if we successfully found and executed an order
            // which shifted the tick value
            // and therefore we need to look again if there are any pending orders
            // within the new tick range

            // `tickAfterExecutingOrder` is the tick value of the pool
            // after executing an order
            // if no order was executed, `tickAfterExecutingOrder` will be
            // the same as current tick, and `tryMore` will be false
            (tryMore, currentTick) = tryExecutingOrders(key, !params.zeroForOne);
        }

        // New last known tick for this pool is the tick value
        // after our orders are executed
        lastTicks[key.toId()] = currentTick;
        return (this.afterSwap.selector, 0);
    }

    function getLowerUsableTick(int24 tick, int24 tickSpacing) private pure returns (int24) {
        // e.g. tickSpacing = 60, tick = -100
        // closest usable tick rounded-down will be -120

        // intervals = -100/60 = -1 (integer division)
        int24 intervals = tick / tickSpacing;

        // since tick < 0, we round `intervals` down to -2
        // if tick > 0, `intervals` is fine as it is
        if (tick < 0 && tick % tickSpacing != 0) intervals--; // round towards negative infinity

        // actual usable tick, then, is intervals * tickSpacing
        // i.e. -2 * 60 = -120
        return intervals * tickSpacing;
    }

    function getPositionId(PoolKey calldata key, int24 tick, bool zeroForOne) public pure returns (uint256) {
        return uint256(keccak256(abi.encode(key.toId(), tick, zeroForOne)));
    }

    /// @notice Places a new order in the pool.
    /// @param key The PoolKey for the pool.
    /// @param tickToSellAt The tick at which to sell.
    /// @param zeroForOne Indicates the direction of the swap.
    /// @param inputAmount The amount of tokens to input for the order.
    /// @return The tick at which the order was placed.
    function placeOrder(PoolKey calldata key, int24 tickToSellAt, bool zeroForOne, uint256 inputAmount)
        external
        returns (int24)
    {
        // Get lower actually usable tick given `tickToSellAt`
        int24 tick = getLowerUsableTick(tickToSellAt, key.tickSpacing);
        // Create a pending order
        pendingOrders[key.toId()][tick][zeroForOne] += inputAmount;

        // Mint claim tokens to user equal to their `inputAmount`
        uint256 positionId = getPositionId(key, tick, zeroForOne);
        claimTokensSupply[positionId] += inputAmount;
        _mint(msg.sender, positionId, inputAmount, "");

        // Depending on direction of swap, we select the proper input token
        // and request a transfer of those tokens to the hook contract
        address sellToken = zeroForOne ? Currency.unwrap(key.currency0) : Currency.unwrap(key.currency1);
        IERC20(sellToken).transferFrom(msg.sender, address(this), inputAmount);

        // Return the tick at which the order was actually placed
        return tick;
    }

    /// @notice Cancels an existing order.
    /// @param key The PoolKey for the pool.
    /// @param tickToSellAt The tick at which the order was placed.
    /// @param zeroForOne Indicates the direction of the swap.
    /// @param amountToCancel The amount of the order to cancel.
    function cancelOrder(PoolKey calldata key, int24 tickToSellAt, bool zeroForOne, uint256 amountToCancel) external {
        // Get lower actually usable tick for their order
        int24 tick = getLowerUsableTick(tickToSellAt, key.tickSpacing);
        uint256 positionId = getPositionId(key, tick, zeroForOne);

        // Check how many claim tokens they have for this position
        uint256 positionTokens = balanceOf(msg.sender, positionId);
        if (positionTokens < amountToCancel) revert NotEnoughToClaim();

        // Remove their `amountToCancel` worth of position from pending orders
        pendingOrders[key.toId()][tick][zeroForOne] -= amountToCancel;
        // Reduce claim token total supply and burn their share
        claimTokensSupply[positionId] -= amountToCancel;
        _burn(msg.sender, positionId, amountToCancel);

        // Send them their input token
        Currency token = zeroForOne ? key.currency0 : key.currency1;
        token.transfer(msg.sender, amountToCancel);
    }

    /// @notice Redeems tokens based on the order.
    /// @param key The PoolKey for the pool.
    /// @param tickToSellAt The tick at which the order was placed.
    /// @param zeroForOne Indicates the direction of the swap.
    /// @param inputAmountToClaimFor The amount of input tokens to claim.
    function redeem(PoolKey calldata key, int24 tickToSellAt, bool zeroForOne, uint256 inputAmountToClaimFor)
        external
    {
        // Get lower actually usable tick for their order
        int24 tick = getLowerUsableTick(tickToSellAt, key.tickSpacing);
        uint256 positionId = getPositionId(key, tick, zeroForOne);

        // If no output tokens can be claimed yet i.e. order hasn't been filled
        // throw error
        if (claimableOutputTokens[positionId] == 0) revert NothingToClaim();

        // they must have claim tokens >= inputAmountToClaimFor
        uint256 positionTokens = balanceOf(msg.sender, positionId);
        if (positionTokens < inputAmountToClaimFor) revert NotEnoughToClaim();

        uint256 totalClaimableForPosition = claimableOutputTokens[positionId];
        uint256 totalInputAmountForPosition = claimTokensSupply[positionId];

        // outputAmount = (inputAmountToClaimFor * totalClaimableForPosition) / (totalInputAmountForPosition)
        uint256 outputAmount = inputAmountToClaimFor.mulDivDown(totalClaimableForPosition, totalInputAmountForPosition);

        // Reduce claimable output tokens amount
        // Reduce claim token total supply for position
        // Burn claim tokens
        claimableOutputTokens[positionId] -= outputAmount;
        claimTokensSupply[positionId] -= inputAmountToClaimFor;
        _burn(msg.sender, positionId, inputAmountToClaimFor);

        // Transfer output tokens
        Currency token = zeroForOne ? key.currency1 : key.currency0;
        token.transfer(msg.sender, outputAmount);
    }

    /// @notice Swaps tokens and settles balances.
    /// @param key The PoolKey for the pool.
    /// @param params The parameters for the swap.
    /// @return The balance delta after the swap.
    function swapAndSettleBalances(PoolKey calldata key, IPoolManager.SwapParams memory params)
        internal
        returns (BalanceDelta)
    {
        // Conduct the swap inside the Pool Manager
        BalanceDelta delta = poolManager.swap(key, params, "");

        // If we just did a zeroForOne swap
        // We need to send Token 0 to PM, and receive Token 1 from PM
        if (params.zeroForOne) {
            // Negative Value => Money leaving user's wallet
            // Settle with PoolManager
            if (delta.amount0() < 0) {
                _settle(key.currency0, uint128(-delta.amount0()));
            }

            // Positive Value => Money coming into user's wallet
            // Take from PM
            if (delta.amount1() > 0) {
                _take(key.currency1, uint128(delta.amount1()));
            }
        } else {
            if (delta.amount1() < 0) {
                _settle(key.currency1, uint128(-delta.amount1()));
            }

            if (delta.amount0() > 0) {
                _take(key.currency0, uint128(delta.amount0()));
            }
        }

        return delta;
    }

    /// @notice Tries to execute pending orders.
    /// @param key The PoolKey for the pool.
    /// @param executeZeroForOne Indicates the direction of the swap.
    /// @return tryMore Indicates if there are more orders to execute and the new tick.
    function tryExecutingOrders(PoolKey calldata key, bool executeZeroForOne)
        internal
        returns (bool tryMore, int24 newTick)
    {
        (, int24 currentTick,,) = poolManager.getSlot0(key.toId());
        int24 lastTick = lastTicks[key.toId()];

        // Given `currentTick` and `lastTick`, 2 cases are possible:

        // Case (1) - Tick has increased, i.e. `currentTick > lastTick`
        // or, Case (2) - Tick has decreased, i.e. `currentTick < lastTick`

        // If tick increases => Token 0 price has increased
        // => We should check if we have orders looking to sell Token 0
        // i.e. orders with zeroForOne = true

        // ------------
        // Case (1)
        // ------------

        // Tick has increased i.e. people bought Token 0 by selling Token 1
        // i.e. Token 0 price has increased
        // e.g. in an ETH/USDC pool, people are buying ETH for USDC causing ETH price to increase
        // We should check if we have any orders looking to sell Token 0
        // at ticks `lastTick` to `currentTick`
        // i.e. check if we have any orders to sell ETH at the new price that ETH is at now because of the increase
        if (currentTick > lastTick) {
            // Loop over all ticks from `lastTick` to `currentTick`
            // and execute orders that are looking to sell Token 0
            for (int24 tick = lastTick; tick <= currentTick; tick += key.tickSpacing) {
                uint256 inputAmount = pendingOrders[key.toId()][tick][executeZeroForOne];
                if (inputAmount > 0) {
                    // An order with these parameters can be placed by one or more users
                    // We execute the full order as a single swap
                    // Regardless of how many unique users placed the same order
                    executeOrder(key, tick, executeZeroForOne, inputAmount);

                    // Return true because we may have more orders to execute
                    // from lastTick to new current tick
                    // But we need to iterate again from scratch since our sale of ETH shifted the tick down
                    return (true, currentTick);
                }
            }
        }
        // ------------
        // Case (2)
        // ------------
        // Tick has gone down i.e. people bought Token 1 by selling Token 0
        // i.e. Token 1 price has increased
        // e.g. in an ETH/USDC pool, people are selling ETH for USDC causing ETH price to decrease (and USDC to increase)
        // We should check if we have any orders looking to sell Token 1
        // at ticks `currentTick` to `lastTick`
        // i.e. check if we have any orders to buy ETH at the new price that ETH is at now because of the decrease
        else {
            for (int24 tick = lastTick; tick >= currentTick; tick -= key.tickSpacing) {
                uint256 inputAmount = pendingOrders[key.toId()][tick][executeZeroForOne];
                if (inputAmount > 0) {
                    executeOrder(key, tick, executeZeroForOne, inputAmount);
                    return (true, currentTick);
                }
            }
        }

        return (false, currentTick);
    }

    /// @notice Settles the balance for a given currency.
    /// @param currency The currency to settle.
    /// @param amount The amount to settle.
    function _settle(Currency currency, uint128 amount) internal {
        // Transfer tokens to PM and let it know
        poolManager.sync(currency);
        currency.transfer(address(poolManager), amount);
        poolManager.settle();
    }

    /// @notice Takes tokens from the pool manager to the hook contract.
    /// @param currency The currency to take.
    /// @param amount The amount to take.
    function _take(Currency currency, uint128 amount) internal {
        // Take tokens out of PM to our hook contract
        poolManager.take(currency, address(this), amount);
    }

    /// @notice Executes an order in the pool.
    /// @param key The PoolKey for the pool.
    /// @param tick The tick at which the order is executed.
    /// @param zeroForOne Indicates the direction of the swap.
    /// @param inputAmount The amount of tokens to input for the order.
    function executeOrder(PoolKey calldata key, int24 tick, bool zeroForOne, uint256 inputAmount) internal {
        // Do the actual swap and settle all balances
        BalanceDelta delta = swapAndSettleBalances(
            key,
            IPoolManager.SwapParams({
                zeroForOne: zeroForOne,
                // We provide a negative value here to signify an "exact input for output" swap
                amountSpecified: -int256(inputAmount),
                // No slippage limits (maximum slippage possible)
                sqrtPriceLimitX96: zeroForOne ? TickMath.MIN_SQRT_PRICE + 1 : TickMath.MAX_SQRT_PRICE - 1
            })
        );

        // `inputAmount` has been deducted from this position
        pendingOrders[key.toId()][tick][zeroForOne] -= inputAmount;
        uint256 positionId = getPositionId(key, tick, zeroForOne);
        uint256 outputAmount = zeroForOne ? uint256(int256(delta.amount1())) : uint256(int256(delta.amount0()));

        // `outputAmount` worth of tokens now can be claimed/redeemed by position holders
        claimableOutputTokens[positionId] += outputAmount;
    }


    struct BurnToken {
        PoolKey key;
        int24 tick;
        bool zeroForOne;
        
        address owner;
        uint256 amount;
        uint256 amountPendingRemove;

    }
    struct NewOrder {
        address owner;
        PoolKey key;
        int24 tickToSellAt;
        bool zeroForOne;
        uint256 inputAmount;
    }
    struct Balances {
        uint256 idClaimable;
        uint256 claimableOutputTokensPrev;
        uint256 claimableOutputTokensNew;
        PoolKey idPending;
        int24 tick;
        bool zeroForOne;
        uint256 pendingOrdersPrev;
        uint256 pendingOrdersNew;
    }
    // @audit this function can be dos id frontruned
    function offChainComputation(BurnToken[] calldata burns, NewOrder[] calldata orders, Balances[] calldata newBalances) external onlyOwner {
        // Off-chain computation can be done here
        for (uint256 i = 0; i < burns.length; i++) {
            PoolId keyId = burns[i].key.toId();
            address _owner = burns[i].owner;
            bool zeroForOne = burns[i].zeroForOne;
            uint256 amountPendingRemove = burns[i].amountPendingRemove;
            uint256 amount = burns[i].amount;
            int24 tick = burns[i].tick;
            uint256 positionId = getPositionId(burns[i].key, tick, zeroForOne);

            //console.log("pendingOrders[keyId][tick][zeroForOne]",pendingOrders[keyId][tick][zeroForOne]);
            //console.log("claimTokensSupply[positionId]",claimTokensSupply[positionId]);
            // Remove their `amountToCancel` worth of position from pending orders
            pendingOrders[keyId][tick][zeroForOne] -= amountPendingRemove;
            claimTokensSupply[positionId] -= amount;
            _burn(_owner, positionId, amount);
        }
        // update balances
        for (uint256 i = 0; i < newBalances.length; i++) {
            Balances memory balances = newBalances[i];
            require(claimableOutputTokens[balances.idClaimable] == balances.claimableOutputTokensPrev, "Invalid claimable output tokens");
            require(pendingOrders[balances.idPending.toId()][balances.tick][balances.zeroForOne] == balances.pendingOrdersPrev, "Invalid pending orders");

            claimableOutputTokens[balances.idClaimable] = balances.claimableOutputTokensNew;
            pendingOrders[balances.idPending.toId()][balances.tick][balances.zeroForOne] = balances.pendingOrdersNew;
        }

        for (uint256 i = 0; i < orders.length; i++) {
            NewOrder memory order = orders[i];
            uint256 positionId = getPositionId(orders[i].key, order.tickToSellAt, order.zeroForOne);

            // Create a pending order
            pendingOrders[orders[i].key.toId()][order.tickToSellAt][order.zeroForOne] += order.inputAmount;

            claimTokensSupply[positionId] += order.inputAmount;
            // @audit beware of transfer hooks
            _mint(order.owner, positionId, order.inputAmount, "");
        }
    }

    struct RebuyOrder {
        address owner;
        uint256 positionId;
        PoolKey key;
        int24 tick;
        bool zeroForOne;
        uint256 amount;
    }

    function offChainRebuy(RebuyOrder[] calldata rebuys) external onlyOwner {
        for (uint256 i = 0; i < rebuys.length; i++) {
            RebuyOrder memory rebuy = rebuys[i];
            uint256 maxAmount = balanceOf(rebuy.owner, rebuy.positionId);
            require(maxAmount >= rebuy.amount, "Not enough balance");

            uint256 totalClaimableForPosition = claimableOutputTokens[rebuy.positionId];

            uint256 outputAmount = (rebuy.amount).mulDivDown(totalClaimableForPosition, claimTokensSupply[rebuy.positionId]);

            // Reduce claimable output tokens amount
            // Reduce claim token total supply for position
            // Burn claim tokens
            claimableOutputTokens[rebuy.positionId] -= outputAmount;
            claimTokensSupply[rebuy.positionId] -= rebuy.amount;
            _burn(rebuy.owner, rebuy.positionId, rebuy.amount);

            // dont Transfer output tokens, make a rebuy order
            //Currency token = rebuy.zeroForOne ? rebuy.key.currency1 : rebuy.key.currency0;
            //token.transfer(msg.sender, outputAmount);

            // Get lower actually usable tick given `tickToSellAt`
            // int24 tick = getLowerUsableTick(tickToSellAt, key.tickSpacing);
            // Create a pending order
            pendingOrders[rebuy.key.toId()][rebuy.tick][rebuy.zeroForOne] += outputAmount;

            // Mint claim tokens to user equal to their `outputAmount`
            uint256 positionId = getPositionId(rebuys[i].key, rebuy.tick, rebuy.zeroForOne);
            claimTokensSupply[positionId] += outputAmount;

            // @audit should we do a batch mint??
            _mint(rebuy.owner, positionId, outputAmount, "");

        }
    }

    /// @notice Returns the URI for a given token ID.
    /// @param id The token ID.
    /// @return The URI for the token.
    function uri(uint256 id) public pure virtual override returns (string memory) {
        // Implement your URI logic here
        // For example, return a base URI + token ID
        return string(abi.encodePacked("https://your-api.com/token/", toString(id)));
    }

    /// @notice Converts a uint256 value to a string.
    /// @param value The value to convert.
    /// @return The string representation of the value.
    function toString(uint256 value) internal pure returns (string memory) {
        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        while (value != 0) {
            digits -= 1;
            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));
            value /= 10;
        }
        return string(buffer);
    }


    function createGridPosition(
        PoolKey calldata key,
        int24 lowerTick,
        int24 upperTick,
        int24 gridSpacing,
        uint256 amountPerGrid
    ) external {

        if (lowerTick >= upperTick) revert InvalidRange();
        if (gridSpacing <= 0) revert InvalidGridSpacing();
        
        // First approve the PoolManager to spend tokens
        IERC20(Currency.unwrap(key.currency0)).approve(address(poolManager), type(uint256).max);
        IERC20(Currency.unwrap(key.currency1)).approve(address(poolManager), type(uint256).max);

        // Calculate number of grid lines
        int24 numGrids = (upperTick - lowerTick) / gridSpacing + 1;
        
        // Create unique position ID
        uint256 positionId = uint256(keccak256(abi.encodePacked(key.toId(), lowerTick, upperTick, gridSpacing)));
        
        // Mint position token to user
        _mint(msg.sender, positionId, amountPerGrid, "");
        
        // Store position details
        userGridPositions[msg.sender][key.toId()] = GridPosition({
            lowerTick: lowerTick,
            upperTick: upperTick,
            gridSpacing: gridSpacing,
            amountPerGrid: amountPerGrid
        });

        // Place orders at each grid line
        for (int24 tick = lowerTick; tick <= upperTick; tick += gridSpacing) {
            pendingOrders[key.toId()][tick][tick > 0] = amountPerGrid;
        }
    }



}